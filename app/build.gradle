apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'//To pass data safety in nav components
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "today.kinema"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.1"

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            ext.enableCrashlytics = true

            minifyEnabled true // Enables code shrinking for the release build type.

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true
            signingConfig signingConfigs.release
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            // Disable fabric build ID generation for debug builds
            debuggable true
            ext.enableCrashlytics = false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.arch.core:core-testing:2.1.0'
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // GLIDE
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //Firebase
    implementation 'com.google.firebase:firebase-crashlytics-ktx:17.2.2'
    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.0'

    //Hilt
    implementation 'com.google.dagger:hilt-android:2.29.1-alpha'
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    kapt 'com.google.dagger:hilt-android-compiler:2.29.1-alpha'
    kapt "androidx.hilt:hilt-compiler:1.0.0-alpha02"

    //Material Design
    implementation 'com.google.android.material:material:1.2.1'

    //Timber - logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // RETROFIT -- keep this version to work with sdk >= 19
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // MockWebServer -- keep this version to work with sdk >= 19
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    //Fused location requires the use of the Google Play SDK
    implementation "com.google.android.gms:play-services-location:17.1.0"

    //Multidex
    implementation "androidx.multidex:multidex:$multidex_version"

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13.1'
    // Mockito framework
    testImplementation 'org.mockito:mockito-core:2.28.2'
    // Module kotlinx-coroutines-test
    testImplementation "io.mockk:mockk:1.10.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.0"
}
